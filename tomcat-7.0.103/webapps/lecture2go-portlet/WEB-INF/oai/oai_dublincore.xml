<?xml version="1.0" encoding="UTF-8" ?>
<!--  http://www.openarchives.org/OAI/2.0/oai_dc.xsl-->
<xsl:stylesheet 
	xmlns:xsl="http://www.w3.org/1999/XSL/Transform"
	xmlns:doc="http://www.lyncode.com/xoai"
	xmlns:dc="http://purl.org/dc/elements/1.1/"
	exclude-result-prefixes="doc"
	version="1.0">
	<xsl:output omit-xml-declaration="yes" method="xml" indent="yes" />
	
	<xsl:template match="/doc:metadata">
		<oai_dc:dc xmlns:dc="http://purl.org/dc/elements/1.1/"
           xmlns:oai_dc="http://www.openarchives.org/OAI/2.0/oai_dc/"
           xmlns:xsi="http://www.w3.org/2001/XMLSchema-instance"
           xsi:schemaLocation="http://www.openarchives.org/OAI/2.0/oai_dc/ http://www.openarchives.org/OAI/2.0/oai_dc.xsd">
           
           <xsl:apply-templates select="doc:element[@name='title']/doc:field[@name='title']"/>
           <xsl:apply-templates select="doc:element[@name='creators']"/>
           <xsl:apply-templates select="doc:element[@name='publisher']/doc:field[@name='publisher']"/>
           <xsl:apply-templates select="doc:element[@name='generationDate']/doc:field[@name='generationDate']"/>
           <xsl:apply-templates select="doc:element[@name='urlIdentifier']/doc:field[@name='urlIdentifier']"/>
           <xsl:apply-templates select="doc:element[@name='tags']"/>
           <xsl:apply-templates select="doc:element[@name='description']/doc:field[@name='description']"/>
           <xsl:apply-templates select="doc:element[@name='dataManager']/doc:field[@name='dataManager']|doc:element[@name='producer']/doc:field[@name='producer']"/>
	  		<xsl:apply-templates select="doc:element[@name='language']/doc:field[@name='language']"/>
	  		<xsl:apply-templates select="doc:element[@name='category']/doc:field[@name='category']"/>
	  		<xsl:apply-templates select="doc:element[@name='containerFormat']/doc:field[@name='containerFormat']"/>
	  		<xsl:apply-templates select="doc:element[@name='duration']/doc:field[@name='duration']"/>
	  		<xsl:apply-templates select="doc:element[@name='licenseShortIdentifier']/doc:field[@name='licenseShortIdentifier']"/>
	  		<xsl:apply-templates select="doc:element[@name='seriesTitle']/doc:field[@name='seriesTitle']|doc:element[@name='seriesUrl']/doc:field[@name='seriesUrl']"/>


<!--
   <dc:rights>Creative Commons Attribution-NonCommercial-NoDerivs 3.0 Unported license (CC-BY-NC-ND)</dc:rights>
-->
       

       </oai_dc:dc>
	</xsl:template>

	<!-- title -->
	<xsl:template match="doc:element[@name='title']/doc:field[@name='title']">
		<dc:title><xsl:value-of select="."/></dc:title>
		<!--TODO: Subtitle <xsl:apply-templates select="/doc:metadata/doc:element[@name='seriesTitle']/doc:field[@name='seriesTitle']"/> -->
	</xsl:template>

	<!-- creators -->
	<xsl:template match="doc:element[@name='creators']">
		<xsl:for-each select="doc:element[@name='creators']">
			<dc:creator>
				<xsl:value-of select="doc:element[@name='lastName']/doc:field[@name='lastName']"/>
				<xsl:value-of select="concat(', ',doc:element[@name='firstName']/doc:field[@name='firstName'])"/>
				<xsl:if test="doc:element[@name='middleName']/doc:field[@name='middleName']">
					<xsl:value-of select="concat(' ',doc:element[@name='middleName']/doc:field[@name='middleName'])"/>
				</xsl:if>
			</dc:creator>
		</xsl:for-each>
	</xsl:template>

	<!-- publisher -->
	<xsl:template match="doc:element[@name='publisher']/doc:field[@name='publisher']">
		<dc:publisher><xsl:value-of select="."/></dc:publisher>
	</xsl:template>

	<!-- date -->
	<xsl:template match="doc:element[@name='generationDate']/doc:field[@name='generationDate']">
		<dc:date>
			<xsl:value-of select="substring(.,1,4)"/>
		</dc:date>
	</xsl:template>

	<!-- identifier -->
	<xsl:template match="doc:element[@name='urlIdentifier']/doc:field[@name='urlIdentifier']">
		<dc:identifier><xsl:value-of select="."/></dc:identifier>
	</xsl:template>

	<!-- subject -->
	<xsl:template match="doc:element[@name='tags']">
		<xsl:for-each select="doc:element[@name='tags']">
			<dc:subject>
				<xsl:value-of select="doc:field[@name='tags']"/>
			</dc:subject>
		</xsl:for-each>
	</xsl:template>

	<!-- description -->
	<xsl:template match="doc:element[@name='description']/doc:field[@name='description']">
		<dc:description><xsl:value-of select="."/></dc:description>
	</xsl:template>

	<!-- contributor - dataManager -->
	<xsl:template match="doc:element[@name='dataManager']/doc:field[@name='dataManager']|doc:element[@name='producer']/doc:field[@name='producer']">
		<dc:contributor><xsl:value-of select="."/></dc:contributor>
	</xsl:template>

	<!-- language -->
	<xsl:template match="doc:element[@name='language']/doc:field[@name='language']">
		<dc:language>
			<xsl:value-of select="substring(.,1,2)"/>
		</dc:language>
	</xsl:template>

	<!-- type -->
	<xsl:template match="doc:element[@name='category']/doc:field[@name='category']">
		<dc:type>
			<xsl:value-of select="."/>
		</dc:type>
	</xsl:template>

	<!-- type -->
	<xsl:template match="doc:element[@name='containerFormat']/doc:field[@name='containerFormat']">
		<xsl:choose>
			<xsl:when test="/doc:metadata/doc:element[@name='containerFormat']/doc:field[@name='containerFormat'] = 'mp4'">
				<dc:type>MovingImage</dc:type>
				<dc:format>video/mpeg</dc:format>
			</xsl:when>
			<xsl:when test="/doc:metadata/doc:element[@name='containerFormat']/doc:field[@name='containerFormat'] = 'mp3'">
				<dc:type>Sound</dc:type>
				<dc:format>audio/mpeg</dc:format>
			</xsl:when>
		</xsl:choose>
	</xsl:template>

	<!-- format - duration -->
	<xsl:template match="doc:element[@name='duration']/doc:field[@name='duration']">
		<dc:format><xsl:value-of select="substring(.,1,8)"/></dc:format>
	</xsl:template>


	<!-- rights -->
	<xsl:template match="doc:element[@name='licenseShortIdentifier']/doc:field[@name='licenseShortIdentifier']">
		<dc:rights><xsl:value-of select="/doc:metadata/doc:element[@name='licenseFullName']/doc:field[@name='licenseFullName']"/></dc:rights>
	</xsl:template>

	<!-- relation -->
	<xsl:template match="/doc:metadata/doc:element[@name='seriesTitle']/doc:field[@name='seriesTitle']|/doc:metadata/doc:element[@name='seriesUrl']/doc:field[@name='seriesUrl']">
		<dc:relation><xsl:value-of select="."/></dc:relation>
	</xsl:template>

</xsl:stylesheet>
